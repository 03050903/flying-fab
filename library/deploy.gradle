apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

ext {
    siteUrl = 'https://github.com/sstoyanova/flying-fab/'
    gitUrl = 'https://github.com/sstoyanova/flying-fab/'
    groupId = 'bg.devlabs.flyingfab'
    artifactId = 'library'
    libname = "flying-fab" // actually not used
    libVersion = "0.0.3" // actually not used
}
version = libVersion
group = groupId

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            groupId groupId
            artifactId artifactId
            version libVersion
            name libname // pom.project.name must be same as bintray.pkg.name
            url siteUrl
            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl
            }
        }
    }
}

bintray {
    user = 'your_bintray_user'
    key = 'your_api_key'
    configurations = ['archives']
    pkg {
        repo = "flyingfab"
        name = libname
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        publish = true
        version {
            name = libVersion
        }
    }
}
if (project.hasProperty("kotlin")) { //Kotlin libraries
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc, dependsOn: dokka) {

    }
} else if (project.hasProperty("android")) {
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }
} else { // Java libraries
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}


task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
    // options.encoding = 'UTF-8'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}